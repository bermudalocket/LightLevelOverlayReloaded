buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'http://repo.spongepowered.org/maven' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        //Change jitpack to moderniser when Forge uploads 1.13.2 MCPConfig
        classpath 'com.github.Chocohead:ForgeGradle:jitpack-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.johnrengelman:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

group 'com.mmyzd.llor'
version '1.13-SNAPSHOT'
archivesBaseName = 'LightLevelOverlayReloaded'

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven { url 'https://www.dimdev.org/maven/' }
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    implementation 'com.github.Chocohead:Rift:jitpack-SNAPSHOT:dev'
    //compile group: 'org.yaml', name: 'snakeyaml', version: '1.24'
    shade 'org.yaml:snakeyaml:1.24'
}

minecraft {
    version = '1.13.2'
    mappings = 'snapshot_20181130'
    runDir = 'run'
    tweakClass = 'org.dimdev.riftloader.launch.RiftLoaderClientTweaker'
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.llor.refmap.json'
}

artifacts {
    shadowJar
}

jar {
    configurations.shade.each { dep ->
        from (project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}